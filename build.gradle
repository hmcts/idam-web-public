import java.util.stream.Collectors

plugins {
  id 'java'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.5.RELEASE' apply false
  id 'org.owasp.dependencycheck' version '3.1.2'
  id 'org.sonarqube' version '2.6.2'
  id 'org.springframework.boot' version '1.5.7.RELEASE' apply false
}

allprojects  {
  apply plugin: 'java'
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'org.owasp.dependencycheck'
  apply plugin: 'war'
  apply plugin: 'org.springframework.boot'

  group = 'uk.gov.hmcts.reform.idam'
  description = 'idam-web-public'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  def idamBomVersion = '1.1.0'

  dependencyManagement {
    imports {
      mavenBom "uk.gov.hmcts.reform.idam:idam-bom:${idamBomVersion}"
    }
  }

  repositories {
    maven {
        url 'https://artifactory.reform.hmcts.net/artifactory/libs-release/'
    }
    maven {
       url "https://dl.bintray.com/hmcts/hmcts-maven"
    }
    jcenter()
    mavenLocal()
  }

  dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'false' ? 11 : 0
    suppressionFile = 'dependency-check-suppressions.xml'
  }

  dependencies {
    compile group: 'uk.gov.hmcts.reform.idam', name: 'idam-api-spec'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    compile group: 'org.springframework.boot', name: 'spring-boot-actuator-docs'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security'
    compile group: 'org.springframework.security', name: 'spring-security-taglibs'
    compile group: 'org.projectlombok', name: 'lombok'
    compile group: 'javax.servlet', name: 'jstl'
    compile group: 'javax.json', name: 'javax.json-api'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
    compile group: 'com.nimbusds', name: 'nimbus-jose-jwt'
    compile group: 'org.apache.httpcomponents', name: 'httpclient'
    compile group: 'org.apache.httpcomponents', name: 'httpcore'
    compile group: 'org.apache.commons', name: 'commons-text'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-tomcat'
    compile group: 'org.apache.tomcat.embed', name: 'tomcat-embed-jasper'
    // TODO mockito version is not correctly resolved from IdAM BOM. Remove version when this is fixed
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.8.9'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-devtools'
    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test') {
        exclude(module: 'commons-logging')
    }
  }

  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
  }

  bootRun {
    systemProperties = System.properties
  }

  task smoke(type: Test) {
    group = 'Delivery pipeline'
    description = 'Executes non-destructive smoke tests against a running instance'
  }

  task functional(type: Test) {
    group = 'Delivery pipeline'
    description = 'Executes functional tests against a running instance'
  }
}

project.tasks['sonarqube'].dependsOn test

def listFiles(String pattern) {
    return new FileNameFinder()
        .getFileNames("${project.rootDir}", pattern)
        .stream()
        .collect(Collectors.joining(","))
}

sonarqube {
  properties {
    property "sonar.projectName", "SIDAM-WEB-PUBLIC"
    property "sonar.jacoco.reportPath", "${listFiles('**/test.exec')}"
    property "sonar.exclusions", "**/uk/gov/hmcts/reform/idam/web/config/properties/*.java," +
            "**/uk/gov/hmcts/reform/idam/web/model/*.java," +
            "**/uk/gov/hmcts/reform/idam/web/helper/MvcKeys.java," +
            "**/uk/gov/hmcts/reform/idam/web/Application.java," +
            "**/*Exception.java"
    }
}
